# Statistical Analysis System (SAS)

SAS can perform the following tasks:

1. It allows you to enter, retrieve and manage your data easily
2. It can read data from various external sources (Excel, CSV, Text files, Databases, Webpage etc)
3. You can explore and manipulate data in SAS.
4. It can analyze your data statistically and mathematically. Includes various statistical techniques.
5. It can generate beautiful graphs and tables.
6. You can run SQL queries on SAS datasets.
7. You can automate repetitive tasks with SAS Macros.
8. It can develop entirely new software applications.

# Reference Links

- [SAS Tutorial](https://www.listendata.com/p/sas-tutorials.html)

# SAS Interface:

![1715180606010](image/SAS_notes/1715180606010.png)

## Basic SAS Program:

![1715180645087](image/SAS_notes/1715180645087.png)

- `data example1;`: This line initiates a data step and creates a new dataset named "example1" to store the data.
- `input name $ ID;`: This line defines the variables for the dataset. It specifies that there are two variables: "name" and "ID." The dollar sign ($) after "name" indicates that it is a character variable, and the lack of a dollar sign after "ID" indicates that it is a numeric variable.
- `cards;`: This keyword signals the start of inline data. The data enclosed between `cards;` and the next semicolon (`;`) will be read as input data for the dataset.
- The lines following `cards;` contain the actual data values. Each line represents one observation (row) in the dataset. In this example, there are four observations with two variables each: "name" and "ID."
- `proc print;`: This line initiates the `proc print` procedure, which is used to display the contents of a dataset.
- `run;`: This line signifies the end of the data step and the end of the `proc print` procedure.

## Log Window:

![1715180721311](image/SAS_notes/1715180721311.png)

## Results Window:

![1715180734879](image/SAS_notes/1715180734879.png)

## Libraries

SAS files are stored in a SAS library. A SAS library is simply a collection of SAS files (data sets) that are stored in a folder. SAS files are stored either temporarily or permanently. By default, it is stored in a temporary library called `Work`.

![1715180805241](image/SAS_notes/1715180805241.png)

### How to create a SAS library

1. **Temporary :** When you don't specify a library name at all, then the file is stored in a temporary SAS library called `Work`. When you close out the SAS session in which you created the SAS file, the temporary library and all of its files are removed from your computer's memory.

> data example;

_In this case, example is a data set that is stored in `Work` library._ **Note :** You can specify the library name Work followed by **dot (.)** and then data set name.

> data work.example;

2. **Permanent :** If you use a library name other than the default library name 'Work' when creating a SAS file, then the file is stored permanently until you delete it. That is, you can use permanent SAS libraries and their contents in subsequent SAS sessions.

You can specify the library name followed by **dot (.) sign** and then data set name.

> data mydata.example;

_In this case, example1 is a data set that is stored in **mydata** library._

## SAS Programming Rules

When it comes to programming in SAS, there are several best practices and rules that can help improve code readability, efficiency, and maintainability. Here are some important SAS programming rules to keep in mind:

### I. Rules for SAS statements

1. All SAS statements (except those containing data) must end with a **semicolon (;)** .
   > **Example :** "DATA example1 **;** " is an example of a SAS statement.
2. Any number of SAS statements can appear on a single line provided they are separated by a **semicolon** .
   > **Example :** "DATA example1; Input Name $ ID;" is an example of a SAS statement.
3. SAS statements typically begin with a SAS keyword. (DATA, PROC)
4. SAS statements are not case sensitive, that is, they can be entered in lowercase, uppercase, or a mixture of the two.
   > **Example :** SAS keywords (DATA, PROC) are not case sensitive
5. A delimited comment begins with a forward slash-asterisk (/_) and ends with an asterisk-forward slash (_/). All text within the delimiters is ignored by SAS.

### II. Rules for SAS names

- All names must contain between 1 and 32 characters.
- The first character appearing in a name must be a letter (A, B, ...Z, a, b, ... z) or an underscore (\_). Subsequent characters must be letters, numbers, or underscores. That is, no other characters, such as $, %, or & are permitted. Blanks also cannot appear in SAS names.
- SAS names are not case sensitive, that is, they can be entered in lowercase, uppercase, or a mixture of the two. (SAS is only case sensitive within quotation marks.)

### III. Rules for SAS variables

> If the variable in the **INPUT** statement is followed by a dollar sign ( **$** ), SAS assumes this is a character variable. Otherwise, the variable is considered as a numeric variable.

## Difference between PROC and DATA Step

**DATA STEP**Any portion of a SAS program that begins with a **DATA statement** and ends with a RUN statement is called a **DATA Step** .

DATA steps are used to manage data. In detail, DATA steps are used to read raw or external data into a SAS data set, to modify data values, and to subset or merge data sets.

**PROC (Procedure)** Any portion of a SAS program that begins with a **PROC statement** and ends with a RUN statement is called a **PROC Step or Procedures** .

PROC steps are in-built programs that allow us to analyze the data contained in a SAS data set. PROC steps are used to calculate descriptive statistics, to generate summary reports, and to create summary graphs and charts.

# Getting Started in SAS Actual User Guide

## Importing Data

### Syntax : PROC IMPORT

Syntax of PROC IMPORT is defined below -

```
PROC IMPORT DATAFILE=FileName OUT=SASDatasetName
DBMS=identifier REPLACE;
GETNAMES=Yes;
RUN;
```

Arguments of PROC IMPORT : Explanation

1. **DATAFILE:** Specify the location of the file to be imported.
2. **OUT:** Specify the name to assign to the dataset after it is imported into SAS
3. **DBMS:** Define the format of the file being imported. Some of the common values are CSV, EXCEL, TAB, DLM, ACCESS.
4. **REPLACE:** Determine whether to replace the existing SAS Dataset. Yes/No.
5. **GETNAMES:** Specify whether to use the first row as variable names. By default it it YES. If you set the option as NO, it will tell SAS not to use the first row of data as variable names. In this case SAS assigns variable names as VAR1, VAR2, VAR3 if there are 3 variables.

File Formats supported in PROC IMPORT

Following is a list of file extensions supported in PROC IMPORT. Specify the value in `DBMS=identifier`option

| Identifier | Output Data Source                | Extension                |
| ---------- | --------------------------------- | ------------------------ |
| CSV        | Comma separated values            | .csv                     |
| DLM        | Delimited file                    | .dat or .txt             |
| TAB        | Tab delimited values              | .txt                     |
| XLS        | Excel 97-2003 workbooks           | .xls                     |
| XLSX       | Microsoft Excel 2007 and later    | .xlsx                    |
| EXCEL      | Supports all versions of MS Excel | .xls, .xlsx, xlsb, .xlsm |
| ACCESS     | Microsoft Access 2000 and later   | .mdb                     |
| SAV        | SPSS file                         | .sav                     |
| DTA        | Stata File                        | .dta                     |
| JMP        | JMP files                         | .jmp                     |

### Use PROC IMPORT to Import CSV File

Let's take a simple example to import CSV (comma-separated values) file into SAS. Here we have data named `data.csv` which has 3 variables and 5 observations. Column names are ID, Name, Score. Data looks like the image below. You can open CSV file in Notepad.

[![PROC IMPORT - SAS](https://blogger.googleusercontent.com/img/b/R29vZ2xl/AVvXsEhMkgMa1yZJKs7awP9kNGsLz9wcpqR2-Qr1lILKKKZAkhFO_DkAPSRWYd0HU3IUdRs6dEfslPQtFbztMeASShS-q9apzdaBF7B-0Jjie1Ff-Zvlc8onOZxkTl-Ml5I0RhetQp-zVGcX8-aTGSHCnGl2iZkte32ivg76iP3ufYxfHOkgf6hxfC2i1aHhuw/s1600-rw/csv.png)](https://blogger.googleusercontent.com/img/b/R29vZ2xl/AVvXsEhMkgMa1yZJKs7awP9kNGsLz9wcpqR2-Qr1lILKKKZAkhFO_DkAPSRWYd0HU3IUdRs6dEfslPQtFbztMeASShS-q9apzdaBF7B-0Jjie1Ff-Zvlc8onOZxkTl-Ml5I0RhetQp-zVGcX8-aTGSHCnGl2iZkte32ivg76iP3ufYxfHOkgf6hxfC2i1aHhuw/s1600/csv.png)

`DBMS=CSV` tells SAS that the file being imported is a CSV file.

```
PROC IMPORT DATAFILE='/home/deepanshu88us0/data.csv'
	DBMS=CSV
	OUT=WORK.READIN;
	GETNAMES=YES;
RUN;
```

The above SAS Program creates a SAS dataset named **READIN** in the temporary library ( **WORK** ).

```
NOTE: WORK.READIN data set was successfully created.
NOTE: The data set WORK.READIN has 5 observations and 3 variables.
```

Output

To see the imported file in **RESULTS** window, you can use `PROC PRINT` procedure. See the SAS program below.

```
PROC PRINT DATA=WORK.READIN;
RUN;
```

[![CSV Output](https://blogger.googleusercontent.com/img/b/R29vZ2xl/AVvXsEj-LTSpSIaISVsgPaerEnkZxPCCMzIEb_zGsm3sm9bUUlQqmhNOyejRhU8rha_T-_sDhILi-Zt3xlx2ScYypr-OHxcNOOZ8BpY2h8zsNh5bsSOwVrJz7-LZ1t0wkckkuHwaqozad9nIDBC3jxUtI9pC_r2ZD5bxZ7fpnwxhdxx-9_JjzXmwmwYpqqLUtQ/s1600-rw/csv2.png)](https://blogger.googleusercontent.com/img/b/R29vZ2xl/AVvXsEj-LTSpSIaISVsgPaerEnkZxPCCMzIEb_zGsm3sm9bUUlQqmhNOyejRhU8rha_T-_sDhILi-Zt3xlx2ScYypr-OHxcNOOZ8BpY2h8zsNh5bsSOwVrJz7-LZ1t0wkckkuHwaqozad9nIDBC3jxUtI9pC_r2ZD5bxZ7fpnwxhdxx-9_JjzXmwmwYpqqLUtQ/s1600/csv2.png)

Use PROC IMPORT to Import Delimited File

Suppose you have a delimeted file and you want to read it in SAS. Delimeter is **|** symbol. Delimited file looks like the image below. Datafile name is **data.txt** .

[![PROC IMPORT : Delimited File](https://blogger.googleusercontent.com/img/b/R29vZ2xl/AVvXsEimmpQsP9kmAarhDpshWo-8ldwLR1q0U0p9A8VwhTDdMZq1dB3kBYNFXjojUGBZcWMxUFKxc78IjGEkGfoBVz6Zib8ocNGyo9jBe0m5YEX7MfBiM7g_9XkaZTLLOhmDBpv0rTDOk7wI1CyNT4vVqPxVzrDZZZ0pDWINC0dffPnwDn46GhWBlTPRzqHiWw/s1600-rw/delimited.png)](https://blogger.googleusercontent.com/img/b/R29vZ2xl/AVvXsEimmpQsP9kmAarhDpshWo-8ldwLR1q0U0p9A8VwhTDdMZq1dB3kBYNFXjojUGBZcWMxUFKxc78IjGEkGfoBVz6Zib8ocNGyo9jBe0m5YEX7MfBiM7g_9XkaZTLLOhmDBpv0rTDOk7wI1CyNT4vVqPxVzrDZZZ0pDWINC0dffPnwDn46GhWBlTPRzqHiWw/s1600/delimited.png)

`DBMS=DLM` tells SAS that the file being imported is a delimited file. To specify delimiter, use the `DELIMITER='|'` option.

```
PROC IMPORT DATAFILE='/home/deepanshu88us0/data.txt'
	DBMS=DLM
	OUT=WORK.READIN REPLACE;
	DELIMITER='|';
	GETNAMES=YES;
RUN;
```

We have used `REPLACE` option to inform SAS to replace the existing SAS dataset **READIN** .

**OUT=WORK.READIN** means **READIN** dataset to be created in the temporary SAS library ( **WORK** ).

> If you have **blank** as delimiter in the file, you don't need to use **DELIMITER** option as default delimeter is blank when you use DBMS=DLM.

### Use PROC IMPORT to Import TAB Delimited File

Suppose you have a TAB delimeted file. TAB delimited file is shown in the image below. Datafile name is **data.txt** .

[![PROC IMPORT : TAB Delimited](https://blogger.googleusercontent.com/img/b/R29vZ2xl/AVvXsEh6fvqUDwtE6lNigMnRIZ5ZCebRnnueINdcWhipdxf2oLJ88Ba47pPkhT3QhUroxQcggVO9nrv-kSfjsb-zeUm_32C7HO2lXGnFrW4zL7R5vh3gqtgovO3Obl3lXgSqYDcFXvR785svS5s6zgJxZbK2E8EJqoizHGXKJi8xgnwwSUBawaondr4gnvTxFQ/s1600-rw/TAB.png)](https://blogger.googleusercontent.com/img/b/R29vZ2xl/AVvXsEh6fvqUDwtE6lNigMnRIZ5ZCebRnnueINdcWhipdxf2oLJ88Ba47pPkhT3QhUroxQcggVO9nrv-kSfjsb-zeUm_32C7HO2lXGnFrW4zL7R5vh3gqtgovO3Obl3lXgSqYDcFXvR785svS5s6zgJxZbK2E8EJqoizHGXKJi8xgnwwSUBawaondr4gnvTxFQ/s1600/TAB.png)

`DBMS=TAB` tells SAS that the file being imported is a TAB delimited file.

```
PROC IMPORT DATAFILE='/home/deepanshu88us0/data.txt'
	DBMS=TAB
	OUT=WORK.READIN REPLACE;
	GETNAMES=YES;
RUN;
```

**Another way** to do this is by specifying `DELIMITER='09'x` and `DBMS=DLM`. '09'x represents the hexadecimal value for the tab character. The tab character (ASCII code 9) is commonly used as a delimiter in data files to separate columns (variables).

```
PROC IMPORT DATAFILE='/home/deepanshu88us0/data.txt'
	DBMS=DLM
	OUT=WORK.READIN REPLACE;
	GETNAMES=YES;
	DELIMITER='09'x;
RUN;
```

### Use PROC IMPORT to import a file with multiple delimiters

While it is uncommon to see files with multiple delimiters, there are instances where certain vendors maintain files in complex formats that may include multiple delimiters. Suppose you have a raw file having **both comma and tab as delimiters** . In this case we need to specify both the delimiters in `DELIMITER=','09'x '`

```
PROC IMPORT DATAFILE='/home/deepanshu88us0/data.txt'
	DBMS=DLM
	OUT=WORK.READIN REPLACE;
	GETNAMES=YES;
	DELIMITER=','09'x ';
RUN;
```

### Use PROC IMPORT to Import Excel File

Many businesses and organizations commonly work with MS Excel files for various purposes such as data storage, analysis, reporting etc. Hence MS Excel file formats are widely used in the real world. In the example below we are importing Excel file in SAS.

[![PROC IMPORT: Excel](https://blogger.googleusercontent.com/img/b/R29vZ2xl/AVvXsEg0ziROuPIb2GWhT46acDS9pxaxtfwsmpVMg3zUJlbbT_2pPVknJGvQAjvZb_vbTJyUI1unTykZfqo53XuhWZhftUA91G_noEqDqXpaxKRd1xdlTu0vGlrqNXwzgC-rhtUnfCGWTdKtPquT4vNXjd6-jwJJLpXBznftlvjMQPo1mx-M0ybP8TITEUHAcA/s1600-rw/excel.png)](https://blogger.googleusercontent.com/img/b/R29vZ2xl/AVvXsEg0ziROuPIb2GWhT46acDS9pxaxtfwsmpVMg3zUJlbbT_2pPVknJGvQAjvZb_vbTJyUI1unTykZfqo53XuhWZhftUA91G_noEqDqXpaxKRd1xdlTu0vGlrqNXwzgC-rhtUnfCGWTdKtPquT4vNXjd6-jwJJLpXBznftlvjMQPo1mx-M0ybP8TITEUHAcA/s1600/excel.png)

`DBMS=XLSX` informs SAS that the file being imported is a MS Excel file (with **.xlsx** extension).

```
PROC IMPORT DATAFILE='/home/deepanshu88us0/Data.xlsx'
	DBMS=XLSX
	OUT=WORK.READIN REPLACE;
	GETNAMES=YES;
RUN;
```

Additional Options in PROC IMPORT to Import Excel File

```
PROC IMPORT DATAFILE="filename"
DBMS=identifier
OUT=SASDataset REPLACE;
SHEET="sheetName";
GETNAMES=YES;
DATAROW=N;
RANGE="rangeName";
RUN;
```

- **SHEET:** Specify the name of the sheet in the Excel file from which you want to import data. When you use PROC IMPORT without explicitly mentioning the SHEET option, SAS will automatically import the first sheet of the Excel file by default. If you want to import a specific sheet, you need to explicitly specify the sheet name.
- **DATAROW:** Specify the row number from which you want SAS to import data. If GETNAMES=YES, the DATAROW value must be greater than or equal to 2. If GETNAMES=NO, the DATAROW value must be greater than or equal to 1.
- **RANGE:** Specify the range of Excel file. For e.g. RANGE="Sheet1$A1:D50"

To understand the above options in detail, checkout this tutorial -

### Use PROC IMPORT to Import SPSS File

A few years ago, SPSS used to be the preferred statistical software for survey analysis. If you have a data file in SPSS format (.SAV) and you want to import it into SAS, you can refer to the SAS program below.

`DBMS=SAV` tells SAS that you are importing a file in the SPSS data file format.

```
PROC IMPORT DATAFILE='/home/deepanshu88us0/surveyData.sav'
	DBMS=SAV
	OUT=WORK.READIN REPLACE;
	fmtlib=WORK.FORMATS;
RUN;
```

## PROC EXPORT Syntax

Below is the syntax for PROC EXPORT:

```
proc export data=sas-dataset-name
  outfile='/path/to/output/filename.csv'
  dbms=csv
  replace;
run;
```

Explanation of PROC EXPORT Syntax

- `data=sas-dataset-name`: Name of SAS dataset you want to export.
- `outfile`: File location where you want to save file.
- `dbms`: File format to use for exported file.
- `replace`: Replaces the exported file if it already exists. It is optional argument.

You can use the following DBMS options to export data to different file formats:

- Specify `dbms=XLSX` to export data as an Excel file.
- Specify `dbms=CSV` to export data as a CSV file.
- Specify `dbms=TAB` to export data as a Text file.
- Specify `dbms=SAV` to export data as a SPSS file.
- Specify `dbms=DTA` to export data as a Stata file.
- Specify `dbms=ACCESSCS` to export data as a MS Access file.

## How to export SAS Data to Excel File

The following code shows how to use PROC EXPORT to export the SAS dataset `sashelp.class` to an Excel file named `class.xlsx` located at '/home/deepanshu88us0/Files/'. It specifies the file format as XLSX.

```
proc export data=sashelp.class
  outfile='/home/deepanshu88us0/Files/class.xlsx'
  dbms=xlsx
  replace;
  sheet="Students";
run;
```

[![PROC EXPORT: Excel File](https://blogger.googleusercontent.com/img/b/R29vZ2xl/AVvXsEihnu0_Vi3y-fDVCZMPpMuGtH7P8cfcG6Er77O6zkNWIhaKjkcC98Oo_R-G2gAeO5VbcX6TxAicBJd1HodURccP2iF0OWajuSHVJAhlW14DLb9wykDCP5t-XSdeV6csbB0VkHJA6H4mgPuPIKnRMZ2aehiyfWbaU7a2S1CtvJTO_7KBjatf7x0E704Ady4a/s1600-rw/export.png)](https://blogger.googleusercontent.com/img/b/R29vZ2xl/AVvXsEihnu0_Vi3y-fDVCZMPpMuGtH7P8cfcG6Er77O6zkNWIhaKjkcC98Oo_R-G2gAeO5VbcX6TxAicBJd1HodURccP2iF0OWajuSHVJAhlW14DLb9wykDCP5t-XSdeV6csbB0VkHJA6H4mgPuPIKnRMZ2aehiyfWbaU7a2S1CtvJTO_7KBjatf7x0E704Ady4a/s1600/export.png)

In the `sheet` argument, we specified the name of the sheet within the Excel file as "Students". It is visible in the image above.

## How to export SAS Data to CSV File

Here we are exporting the SAS dataset `sashelp.class` to a CSV file named `class.csv`.

```
proc export data=sashelp.class
  outfile='/home/deepanshu88us0/Files/class.csv'
  dbms=csv
  replace;
run;
```

[![PROC EXPORT: CSV File](https://blogger.googleusercontent.com/img/b/R29vZ2xl/AVvXsEj98LH2buTBgjCHRi7QXI0qrohk8bUt8hR0knd9OzprWTSQgkPj-_S0cyRnt3cWO5JLas-eeDAaE_fCxkCJRFDfzZ74eeHvbgPfYkpqyz6shgST21a94SFEgLVaqu7Ua1H4Miv-IZ383-Qyh8CDimAK1jZKx4XY3T49kjsSC3PHqdmQ9oCk2mpOZ-EHk0W-/s1600-rw/export2.png)](https://blogger.googleusercontent.com/img/b/R29vZ2xl/AVvXsEj98LH2buTBgjCHRi7QXI0qrohk8bUt8hR0knd9OzprWTSQgkPj-_S0cyRnt3cWO5JLas-eeDAaE_fCxkCJRFDfzZ74eeHvbgPfYkpqyz6shgST21a94SFEgLVaqu7Ua1H4Miv-IZ383-Qyh8CDimAK1jZKx4XY3T49kjsSC3PHqdmQ9oCk2mpOZ-EHk0W-/s1600/export2.png)

## How to export SAS Data to Text File

In the code below, we are exporting the SAS dataset `sashelp.class` to a **Tab Delimited file** named `class.txt`.

```
proc export data=sashelp.class
  outfile='/home/deepanshu88us0/Files/class.txt'
  dbms=tab
  replace;
run;
```

[![PROC EXPORT: TEXT File](https://blogger.googleusercontent.com/img/b/R29vZ2xl/AVvXsEgtVoVGwb9pAsCl3fXQj7IyiWyWPn0rLQw4vdnYZzoJOm4GF_EN50y9_QatFW8IYnsmpD9trnSeYlZ76m93lVNYQ3dRVWYwBDFnRTaIY__5R0ymvUWYWBXzBbP3USxQYX_6bpVr9DZLkF6q8eu0p9787YwFlKDOC_Y9fSqf4elqFXU_fsYzMvtuv-0wdoU2/s1600-rw/export3.png)](https://blogger.googleusercontent.com/img/b/R29vZ2xl/AVvXsEgtVoVGwb9pAsCl3fXQj7IyiWyWPn0rLQw4vdnYZzoJOm4GF_EN50y9_QatFW8IYnsmpD9trnSeYlZ76m93lVNYQ3dRVWYwBDFnRTaIY__5R0ymvUWYWBXzBbP3USxQYX_6bpVr9DZLkF6q8eu0p9787YwFlKDOC_Y9fSqf4elqFXU_fsYzMvtuv-0wdoU2/s1600/export3.png)

To exclude headers in the exported file, we can use the argument `PUTNAMES=NO` in PROC EXPORT.

```
proc export data=sashelp.class
  outfile='/home/deepanshu88us0/Files/class.txt'
  dbms=tab
  replace;
  putnames=no;
run;
```

## How to export SAS Data to SPSS File

To export a SAS dataset to a SPSS file, we need to specify the SAV file format, which is the file format used by SPSS software.

```
proc export data=sashelp.class
  outfile='/home/deepanshu88us0/Files/class.sav'
  dbms=sav
  replace;
run;
```

# SAS : CREATING OR MODIFYING A VARIABLE

This tutorial demonstrates how to create or modify a variable. It is a common data requirement to create a new variable based on the existing variable. For example, you have existing prices and you need to adjust inflation rate and create new price structure. To achieve it, you need to create a new variable for new prices. Sometimes, you may be asked not to create new variables for changes but adjust the logic in the existing variables.

**_Let's create a sample dataset_**

In the code below, we are creating a dataset named Example1 which is stored on WORK (temporary) library. In this dataset, there would be a variable called OldPrice which contains a value 10. The RUN statement is used to close the dataset program. Press F3 or click on submit button to make the code run.

> DATA Example1;
> OldPrice=10;
> RUN;

**I. Creating a numeric variable**

You create variables using the form: **variable = expression;**

Suppose you are asked to create a new variable **NewPrice** , in the existing SAS data set **Example1.** Both variables are numeric. The variable NewPrice is twice of OldPrice.

> DATA Example1;
> SET Example1;
> NewPrice=2\*OldPrice;
> RUN;

| [![](https://3.bp.blogspot.com/-7nnphYfDE04/WGTWukRZF8I/AAAAAAAAFvo/Zttk5j0O02kUDolhOE7mjPEZ9cw3f7pEQCLcB/s1600-rw/create%2Ba%2Bnew%2Bvariable.png)](https://3.bp.blogspot.com/-7nnphYfDE04/WGTWukRZF8I/AAAAAAAAFvo/Zttk5j0O02kUDolhOE7mjPEZ9cw3f7pEQCLcB/s1600/create%2Ba%2Bnew%2Bvariable.png) |
| ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------ |
| SAS : Create a numeric variable                                                                                                                                                                                                                                                                  |

If you are asked to store a new variable NewPrice on a **new dataset** , you can create it using **DATA statement** .

> DATA Readin;
> SET Example1;
> NewPrice=2\*OldPrice;
> RUN;

In this case, the dataset READIN was created.

**II. Creating a character variable**

In the same dataset Example1, let's create a character variable say **Type.** The character value for set is set **'Good'** .

**The quote marks needs to be entered around the character variable.**

> DATA Example1;
> SET Example1;
> **Type = 'Good'** ;
> RUN;

Since Type is a character variable, it should in quotes. It can be either single or double quotes.

**III. Creating or Modifying a variable**

Suppose the value of OldPrice is increased by 5 units and you need to calculate the relative change in price. In this case, we are modifying the existing variable OldPrice so we will add 5 to OldPrice. later we calculate the percentage change between old and new price.

> DATA Readin;
> SET Example1;
> OldPrice=5 + OldPrice;
> NewPrice=OldPrice\*2;
> Change= ((NewPrice-OldPrice)/ OldPrice);
> Format Change Percent10.0;
> RUN;

The **FORMAT statement** is used to display the change value in percentage format. In this case, we are creating a new dataset as well.

| [![](https://3.bp.blogspot.com/-ipK-iAtKF88/WGTZXeUp0tI/AAAAAAAAFv0/0gTVRKfrakIWnLPqpIMxDtXcFMpnDntvQCLcB/s1600-rw/Modifying%2BVariable.png)](https://3.bp.blogspot.com/-ipK-iAtKF88/WGTZXeUp0tI/AAAAAAAAFv0/0gTVRKfrakIWnLPqpIMxDtXcFMpnDntvQCLcB/s1600/Modifying%2BVariable.png) |
| ---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------- |
| SAS : Modifying a Variable                                                                                                                                                                                                                                                         |

# DROPPING VARIABLES FROM A DATA SET IN SAS

This post explains how to drop variables from a dataset in SAS. It includes various tricks to delete variables from data.In SAS, there are two ways to handle dropping variables :

- **DROP =** data set option
- **DROP** statement

**Let's start with creating a data set :**

The main differences between the two are as follows :

**I.** **Scenario : Create a new variable based on existing data and then drops the irrelevant variables**

By using the **DROP statement** , we can command SAS to drop variables only at completion of the DATA step.

> data readin;
> set outdata;
> totalsum = sum(obs1,obs2,obs3);
> **drop obs1 obs2 obs3;**
> run;

In the above example, we simply ask SAS sum up all the values in variables **obs1,obs2 and obs3** to produce a new variable **totalsum** and then drop the old variables **obs1,obs2 and obs3** .

**Consequence of using DROP = Option**

data readin;
set outdata **(** **drop = obs1 obs2 obs3)** ;
totalsum = sum(obs1,obs2,obs3);
run;

The variables **obs1,obs2 and obs3** are not available for use after data set **outdata** has been copied into the new data set **readin** . Hence **totalsum** would contain missing values only.

**II. DROP statement can be used anywhere in DATA steps whereas DROP = option must follow the SET statement.**

#### **DROP statement**

> data readin;
> set outdata;
> if gender = 'F';
> **drop age;**
> run;

**OR**

> data readin;
> set outdata;
> **drop age;**
> if gender = 'F';
> run;

#### **DROP = option**

> data readin;
> set outdata **(drop = age)**;
> if gender = 'F';
> run;

**III.** **Scenario : Dropping variables while printing**

**DROP statement** can be used in DATA steps only whereas **DROP = option** can be used in DATA steps and PROC steps (for printing)

> proc print data = outdata **(drop = age)**;
> where gender = 'F';
> run;

# SAS: HOW TO RENAME VARIABLES

This tutorial explains how to rename variables in SAS using the RENAME option, along with examples.

The syntax of RENAME option is as follows :

```
RENAME=(variable1=new_variable1 variable2=new_variable2 ...)
```

## Rename One Variable in SAS

The following code renames the variable "petallength" to "petal_length" in the dataset "sashelp.iris" and create a new dataset called "mydata" with the renamed variable.

```
data mydata (rename=(petallength = petal_length));
set sashelp.iris;
run;
```

To check if a variable has been renamed correctly, we can use PROC CONTENTS to see the variable names of a dataset.

```
proc contents data=sashelp.iris SHORT;
proc contents data=mydata SHORT;
```

[![Rename variables in SAS](https://blogger.googleusercontent.com/img/b/R29vZ2xl/AVvXsEgOYQtCA8faI477KYZmf08calRKPJBmNCc6-1CBZvvqzjpoAtLY6g0rgfgXeuGgNsJJTGmygS32YUMbEj9OEZRnRvdKe6Un-usF5ViPCCt6FekD8UOxibzUdE3saDblIv2bjCJ8xoRms6XVPT44S8rhiV4A-b7JHd2PVt1P9EwPc5KUiREv0V4cYZQm0H08/s1600-rw/rename.png)](https://blogger.googleusercontent.com/img/b/R29vZ2xl/AVvXsEgOYQtCA8faI477KYZmf08calRKPJBmNCc6-1CBZvvqzjpoAtLY6g0rgfgXeuGgNsJJTGmygS32YUMbEj9OEZRnRvdKe6Un-usF5ViPCCt6FekD8UOxibzUdE3saDblIv2bjCJ8xoRms6XVPT44S8rhiV4A-b7JHd2PVt1P9EwPc5KUiREv0V4cYZQm0H08/s1600/rename.png)

When should the RENAME option be used - DATA or SET Statement?

The RENAME option can be used with either the DATA statement or the SET statement, depending on your specific needs.

- **RENAME option in the DATA statement:** When you use the RENAME option in the DATA statement, you are renaming variables in the output dataset.
- **RENAME option in the SET statement:** When you use the RENAME option in the SET statement, you are renaming variables while reading data from an input dataset.

Incorrect CodeThe following code returns missing values for a new variable "newvar" because "petal_length" was not renamed before creating the new variable "newvar". Hence `rename=` option should be used in the SET statement instead of DATA statement here.

```
data mydata (rename=(petallength = petal_length));
set sashelp.iris;
newvar = petal_length * 10;
run;
```

Correct Code

```
data mydata;
set sashelp.iris (rename=(petallength = petal_length));
newvar = petal_length * 10;
run;
```

## Rename Multiple Variables in SAS

The following code renames the variables "petallength" to "petal_length" and "sepallength" to "sepal_length" while reading the data from the "sashelp.iris" dataset.

```
data mydata;
set sashelp.iris (rename=(petallength = petal_length sepallength=sepal_length));
run;

proc contents data=sashelp.iris SHORT;
proc contents data=mydata SHORT;
```

## Rename All Variables in SAS

The following code renames all variables in the "sashelp.iris" dataset by adding the suffix "\_1" to their original names.

```
proc sql noprint;
select cats(name,"=",name,"_1")
into :rename_vars separated by " "
from dictionary.columns
where libname="SASHELP"
and memname="IRIS";
quit;

%PUT &rename_vars.;

data mydata;
set sashelp.iris (rename=(&rename_vars.));
run;
```

- The above code uses the table `dictionary.columns` which contains information about the column names of all datasets in the current SAS session.
- By using the `WHERE` statement, we filtered this table and selected only column names in the "sashelp.iris" dataset and stored them in the macro variable "rename_vars"
- The `%PUT` statement is used to display the contents of the macro variable "rename_vars" in the log. It helps you to check what the macro variable contains.
- In the subsequent DATA step, the SET statement reads data from the "sashelp.iris" dataset and applies the variable rename assignments specified in the macro variable "rename_vars"
- The resulting dataset "mydata" will contain the renamed variables with **suffix "\_1"** .

## Rename Variables based on a Pattern

The following code renames only those variables which contain " **Length** " in their names using **CONTAINS** operator.

```
proc sql noprint;
select cats(name,"=",name,"_1")
into :rename_vars separated by " "
from dictionary.columns
where libname="SASHELP"
and memname="IRIS"
and name contains 'Length';
quit;

%PUT &rename_vars.;

data mydata;
set sashelp.iris (rename=(&rename_vars.));
run;
```

# HOW TO USE NOT EQUAL TO CONDITION IN SAS

This tutorial explains various ways to use the 'not equal to' operator in SAS, along with examples.

1. ^= Operator

In SAS, **^=** operator means "not equal to". In this example, we are telling SAS to filter out Audi cars from the "cars" dataset in the sashelp library.

```
data outdata;
set sashelp.cars;
if make ^= "Audi";
run;
```

[]()[![Not Equal To Operator in SAS](https://blogger.googleusercontent.com/img/b/R29vZ2xl/AVvXsEjqkZV0Nlr-TucbEyioLyNIui94Qdy8M_lv_9zDbE5YBWrcEluRYMmG74xkya9oJQPtJCnpHv6vX5WTWMyMp7G7tS8n1SM0bkofA4IC9XBJ73ceDiigYYjkg3KW0Ivzbg1g2DG7GB_rozm-OC7oq_Im30AAVBWll5_6JfrQ2r9_J7sRcNfDakietA0Zo0ib/s1600-rw/not_equal.png)](https://blogger.googleusercontent.com/img/b/R29vZ2xl/AVvXsEjqkZV0Nlr-TucbEyioLyNIui94Qdy8M_lv_9zDbE5YBWrcEluRYMmG74xkya9oJQPtJCnpHv6vX5WTWMyMp7G7tS8n1SM0bkofA4IC9XBJ73ceDiigYYjkg3KW0Ivzbg1g2DG7GB_rozm-OC7oq_Im30AAVBWll5_6JfrQ2r9_J7sRcNfDakietA0Zo0ib/s1600/not_equal.png)

Similarly, you can use the ^= operator in the **IF-THEN-ELSE statement** . In this case, we are creating a binary flag (1/0) for Audi cars. It returns 1 against Audi cars else 0.

```
data outdata;
set sashelp.cars;
if make ^= "Audi" then Audi = 0;
else Audi = 1;
run;
```

2. NE Symbol

In SAS, **ne** symbol means "not equal to".

```
data outdata;
set sashelp.cars;
if make ne "Audi";
run;
```

3. ~= Operator

In SAS, **~=** symbol means "not equal to".

```
data outdata;
set sashelp.cars;
if make ~= "Audi";
run;
```

4. NOT IN Operator

The benefit of the **NOT IN** operator in SAS is that it can exclude multiple values.

```
data outdata;
set sashelp.cars;
if make not in ("Audi");
run;
```

Let's say you want to filter out cars of multiple car makers - Audi and BMW. In this case, NOT IN operator is useful to negate multiple values.

```
data outdata;
set sashelp.cars;
if make not in ("Audi", "BMW");
run;
```

5. <> Operator in WHERE Statement

In a WHERE statement, the " **<>** " symbol is considered as "not equal to". Please make a note that <> is interpreted as the maximum of the two numeric values other than the WHERE statement.

```
data outdata;
set sashelp.cars;
where make <> "Audi";
run;
```

---

# Personal Notes

1. SAS is opened in a browser window and doesnt have its own seperate application to interact with
2. To start a new sas program (aka where you are going to write the code to do anything within SAS), you would click on the new icon under the "Files and Folders" option on the left-hand side of the browser. It is the first icon and looks like a square with an asteriks in the upper right hand corner
3. In order to add comments to your code in SAS you would put the text in between /\* \*/ which can be created easily with the shortcut ctrl+/
4.
